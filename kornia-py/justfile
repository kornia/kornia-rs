@_default:
  just --list

# Check if the binaries required to create the python environment
check-environment:
  @echo "UV version." && uv --version
  @echo "Rust version." && cargo --version

# Setup the Python virtual environment
venv py_version='3.9':
  @echo "ðŸš€ Creating virtual environment using uv"
  [ -d .venv ] || uv venv -p {{ py_version }}


# Create virtual environment, and install dev requirements
install-dev py_version='3.9':
  @just check-environment
  @just venv {{ py_version }}
  @echo "ðŸš€ Installing requirements (for devs)"
  uv pip install -r requirements-dev.txt pip

# Create virtual environment, install dev requirements, and install kornia-py (--editable)
install py_version='3.9':
  @just install-dev {{ py_version }}
  uv pip install -e .

# Test the code with pytest
test py_version='3.9' flags='':
  @echo "ðŸš€ Testing code: Running pytest"
  @just build {{ py_version }} {{ flags }}
  @uv run pytest

# Compile and install kornia-py for development (run maturin develop)
build py_version='3.9' flags='':
  @just install-dev {{ py_version }}
  @echo "ðŸš€ Building kornia-py"
  uv run maturin develop -m Cargo.toml {{ flags }}
